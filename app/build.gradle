import groovy.json.JsonSlurper

apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId "pt.uninova.s4h.citizenhub"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "0.1.0"

        def configFile = file(rootDir.absolutePath + "/d4lsdk-config.json")
        def config = new JsonSlurper().parseText(configFile.text)

        manifestPlaceholders = [
                clientId      : config.id,
                clientSecret  : config.secret,
                environment   : config.environment,
                redirectScheme: config.redirectScheme,
                debug         : config.debug,
                platform      : config.platform
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            matchingFallbacks = ['release']
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugar_version"

    // Appcompat
    implementation "androidx.appcompat:appcompat:$appcompat_version"

    // Constraintlayout
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"

    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // Navigation
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$navigation_version"
    implementation "androidx.navigation:navigation-fragment:$navigation_version"
    implementation "androidx.navigation:navigation-ui:$navigation_version"

    // Recyclerview
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // D4L SDK
    dependencies {
        implementation("care.data4life.hc-sdk-kmp:sdk-android:$d4lsdk_version") {
            exclude group: 'care.data4life.hc-sdk-kmp', module: 'securestore-jvm'
            exclude group: 'care.data4life.hc-sdk-kmp', module: 'crypto-jvm'
            exclude group: 'care.data4life.hc-sdk-kmp', module: 'auth-jvm'
            exclude group: 'care.data4life.hc-util-sdk-kmp', module: 'util-jvm'
            exclude group: 'org.threeten', module: 'threetenbp'
        }
    }

    implementation "care.data4life.hc-fhir-helper-sdk-kmp:fhir-helper-android:$d4lhelpersdk_version"

    // Material Calendar View
    implementation "com.github.prolificinteractive:material-calendarview:$materialcalendarview_version"
    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.0'
}
