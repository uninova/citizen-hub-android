apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "pt.uninova.s4h.citizenhub"
        minSdkVersion 26
        targetSdkVersion 32
        versionCode 120221012
        versionName "0.11.10"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            matchingFallbacks = ['release']
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-debug"
        }
    }

    flavorDimensions "environment"

    productFlavors {
        development
        stable
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    productFlavors.all { flavor ->
        def propertiesFile = rootProject.file("env.${flavor.name}.properties")
        def properties = new Properties()

        properties.load(new FileInputStream(propertiesFile))

        manifestPlaceholders = [
                clientId      : properties["smart4HealthId"],
                clientSecret  : properties["smart4HealthSecret"],
                environment   : properties["smart4HealthEnvironment"],
                redirectScheme: properties["smart4HealthRedirectScheme"],
                debug         : properties["smart4HealthDebug"],
                platform      : properties["smart4HealthPlatform"]
        ]

        buildConfigField("String", "SMART_BEAR_API_KEY", "\"${properties["smartBearApiKey"]}\"")
        buildConfigField("String", "SMART_BEAR_URL", "\"${properties["smartBearUrl"]}\"")
        buildConfigField("String", "SMART4HEALTH_APP_URL", "\"${properties["smart4HealthAppUrl"]}\"")

        flavor.resValue "string", "smart4HealthAppUrl", "${properties["smart4HealthAppUrl"]}"

        if (flavor.name != "stable" && properties["skipAuth"] != null) {
            buildConfigField("Boolean", "SKIP_AUTH", properties["skipAuth"])
        } else {
            buildConfigField("Boolean", "SKIP_AUTH", "false")
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        resources.excludes.add("META-INF/DEPENDENCIES")
    }

    namespace 'pt.uninova.s4h.citizenhub'
}

dependencies {
    def concurrent_version = "1.1.0"
    def d4l_sdk_android_version = "1.16.0"
    def d4l_fhir_helper_sdk_version = "1.9.0"
    def desugar_version = "1.1.5"
    def fragment_version = "1.5.2"
    def hapi_fhir_version = "6.0.2"
    def lifecycle_version = "2.5.1"
    def preference_version = "1.2.0"
    def okhttp_version = "4.9.3"
    def work_version = "2.7.1"

    implementation(project(":data"))
    implementation(project(":persistence"))
    implementation(project(":util"))

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugar_version"

    implementation "androidx.concurrent:concurrent-futures:$concurrent_version"

    implementation "androidx.fragment:fragment:$fragment_version"

    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    implementation "androidx.preference:preference:$preference_version"

    implementation "androidx.work:work-runtime:$work_version"

    implementation "ca.uhn.hapi.fhir:hapi-fhir-android:$hapi_fhir_version"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-structures-r4:$hapi_fhir_version"

    implementation("care.data4life.hc-sdk-kmp:sdk-android:$d4l_sdk_android_version") {
        exclude group: 'care.data4life.hc-sdk-kmp', module: 'securestore-jvm'
        exclude group: 'care.data4life.hc-sdk-kmp', module: 'crypto-jvm'
        exclude group: 'care.data4life.hc-sdk-kmp', module: 'auth-jvm'
        exclude group: 'care.data4life.hc-util-sdk-kmp', module: 'util-jvm'
    }

    implementation "care.data4life.hc-fhir-helper-sdk-kmp:fhir-helper-android:$d4l_fhir_helper_sdk_version"

    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"

    //

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation "androidx.appcompat:appcompat:$appcompat_version"

    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"

    implementation "androidx.navigation:navigation-dynamic-features-fragment:$navigation_version"
    implementation "androidx.navigation:navigation-fragment:$navigation_version"
    implementation "androidx.navigation:navigation-ui:$navigation_version"

    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"



    // Material Calendar View
    implementation 'pt.uninova.s4h:material-calendarview:2.0.1'

    implementation 'com.google.android.gms:play-services-wearable:17.1.0'

    implementation 'com.android.support:support-annotations:28.0.0'

    // Material to design graphs in summary details
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    //noinspection GradleCompatible
    implementation 'com.android.support:design:28.0.0'
}